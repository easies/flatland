/* vim: set ft=processing : */

/**
 * A button element.
 */
class Button {
    /* x, y - coordinates
     * w - width
     * h - height */
    int x, y, w, h;
    /* button text */
    String button_text;
    int font_size;
    color fill_color;

    public Button(int x, int y, int w, int h, String button_text,
            int font_size) {
        this.x = x;
        this.y = y;
        this.w = w;
        this.h = h;
        this.button_text = button_text;
        this.font_size = font_size;
        this.fill_color = color(0, 0, 0, 0);
    }

    void draw() {
        pushMatrix();
        stroke(255);
        fill(fill_color);
        rect(x, y, w, h);
        stroke(255);
        fill(255);
        textSize(font_size);
        text(button_text, x, y + font_size);
        popMatrix();
    }

    boolean is_within(int the_x, int the_y) {
        return (x <= the_x && the_x <= x + w &&
            y <= the_y && the_y <= y + h);
    }

    void on_click() {}

    void on_press() {
        this.fill_color = #CCCCCC;
    }

    void on_release() {
        // Set to transparent.
        this.fill_color = color(0, 0, 0, 0);
    }
}

class PlayButton extends Button {
    PlayButton(int x, int y, int w, int h, String s, int fs) {
        super(x, y, w, h, s, fs);
    }
    void on_click() {
        // Call the setup after 100ms.
        setTimeout(setup_world1, 100);
    }
}

class OptionButton extends Button {
    OptionButton(int x, int y, int w, int h, String s, int fs) {
        super(x, y, w, h, s, fs);
    }
    void on_click() {
        world = new OptionWorld();
    }
}

class GenericMenuWorld extends World {
    GenericMenuWorld() {
        super();
    }

    /* Check for hovering. */
    void mouseMoved() {
        for (int i = 0, len = things.size(); i < len; i++) {
            thing = things.get(i);
            if (thing.is_within && thing.is_within(mouseX, mouseY)) {
                cursor(HAND);
                return;
            }
        }
        cursor(ARROW);
    }

    /* Trigger the on_click event. */
    void mouseClicked() {
        for (int i = 0, len = things.size(); i < len; i++) {
            thing = things.get(i);
            if (thing.is_within && thing.is_within(mouseX, mouseY)) {
                thing.on_click();
                cursor(ARROW);
                return;
            }
        }
    }

    /* Trigger the on_press event. */
    void mousePressed() {
        for (int i = things.size() - 1; i >= 0; i--) {
            thing = things.get(i);
            if (thing.is_within && thing.on_press &&
                thing.is_within(mouseX, mouseY)) {
                thing.on_press();
                return;
            }
        }
    }

    /* Trigger the on_release event. */
    void mouseReleased() {
        for (int i = 0, len = things.size(); i < len; i++) {
            thing = things.get(i);
            // XXX Call the on_release callback.
            if (thing.on_release) {
                thing.on_release();
            }
        }
    }
}

class MenuWorld extends GenericMenuWorld {
    MenuWorld() {
        super();
        menux = width / 2;
        things.add(new PlayButton(menux, height / 2, 150, 40, "Play!", 30));
        things.add(new OptionButton(menux, height / 2 + 50, 150, 40,
            "Options", 30));
    }

    void draw() {
        pushMatrix();
        stroke(255);
        fill(255);
        textSize(60);
        text("Flatland Primer", 200, 200);
        popMatrix();
        super.draw();
    }
}

class FPSButton extends Button {
    FPSButton(int x, int y, int w, int h, int fs) {
        super(x, y, w, h, fps_on ? "fps = on" : "fps = off", fs);
    }

    String fps_string() {
        return fps_on ? "fps = on" : "fps = off";
    }

    void on_click() {
        // Toggle fps flag.
        fps_on = !fps_on;
        this.button_text = fps_string();
    }
}

class DebugButton extends Button {
    DebugButton(int x, int y, int w, int h, int fs) {
        super(x, y, w, h, debug ? "debug = on" : "debug = off", fs);
    }

    String debug_string() {
        return debug ? "debug = on" : "debug = off";
    }

    void on_click() {
        // Toggle debug flag.
        debug = !debug;
        this.button_text = debug_string();
    }
}

class BackToMainMenuButton extends Button {
    BackToMainMenuButton(int x, int y, int w, int h, String s, int fs) {
        super(x, y, w, h, s, fs);
    }

    void on_click() {
        setup_menu();
    }
}

class OptionWorld extends GenericMenuWorld {
    OptionWorld() {
        super();
        button_width = 200;
        things.add(new FPSButton(width / 2, height / 2, button_width, 40, 30));
        things.add(new DebugButton(width / 2, height / 2 + 50, button_width,
            40, 30));
        things.add(new BackToMainMenuButton(width / 2, height / 2 + 100,
            button_width, 40, "back", 30));
    }

    void draw() {
        pushMatrix();
        stroke(255);
        fill(255);
        textSize(60);
        text("Options", 200, 200);
        popMatrix();
        super.draw();
    }
}
